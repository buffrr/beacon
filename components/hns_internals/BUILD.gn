import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

tsc_folder = "tsc"

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/beacon_hns_internals_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/beacon_hns_internals_resources.h",
    "grit/beacon_hns_internals_resources_map.cc",
    "grit/beacon_hns_internals_resources_map.h",
    "beacon_hns_internals_resources.pak",
  ]

  resource_ids = "//beacon/browser/resources/resource_ids"
  output_dir = "$root_gen_dir/chrome"
}

generate_grd("build_grd") {
  grd_prefix = "beacon_hns_internals"
  out_grd = "$target_gen_dir/beacon_hns_internals_resources.grd"
  deps = [ ":build_ts" ]
  manifest_files = [ "$target_gen_dir/tsconfig.manifest" ]
  input_files = [ "beacon_hns_internals.html" ]
  input_files_base_dir = rebase_path(".", "//")
}

html_to_js("web_components") {
  js_files = [ "beacon_hns_internals_app.ts" ]
}

copy("copy_proxy") {
  sources = [ "//beacon/components/hns_internals/beacon_hns_internals_proxy.ts" ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

ts_library("build_ts") {
  root_dir = target_gen_dir
  out_dir = "$target_gen_dir/$tsc_folder"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "beacon_hns_internals_app.ts",
    "beacon_hns_internals_proxy.ts",
  ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/js/browser_command:build_ts",
  ]
  extra_deps = [
    ":copy_proxy",
    ":web_components",
  ]
}
